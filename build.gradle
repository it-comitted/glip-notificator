/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'developer' at '08/10/15 1:51 PM' with Gradle 2.7
 *
 * This generated file contains a commented-out sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/2.7/userguide/tutorial_java_projects.html
 */

import java.nio.file.Files
import java.nio.file.Paths

buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
    }

    // Constants for the build
    ext.teamCityHomeFolder = "/Users/developer/Java/TeamCity"
    ext.teamCityBuildServerFolder = "/Users/developer/.BuildServer"
}

allprojects {
    ext.printAndDelete = { dir, fileName ->
        print "Deleting: '" + fileName + "' from '" + dir + "'\n"
        file(dir + fileName).delete()
    }
}

// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'com.github.johnrengelman.shadow'

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()

    maven {
        url "http://repository.jetbrains.com/all"
    }
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compile fileTree(dir: 'lib', include: '*.jar')

    providedCompile 'org.jetbrains.teamcity:server-api:9.0.3'

    testCompile 'junit:junit:4.11',
            'hamcrest:hamcrest:1.12',
            'org.mockito:mockito-all:1.8.4',
            'org.powermock:powermock-api-mockito:1.6.3',
            'org.powermock:powermock-easymock-release-full:1.6.3'

    testRuntime 'org.easymock:easymock:3.4'
}

shadowJar {
    baseName = project.name
    classifier = ''

    dependencies {
        include(dependency('com.goole:*:*'))
    }
}

task buildPlugin(type: Zip) {
    from('packaging') {
        include 'teamcity-plugin.xml'
        into ''
    }

    from(buildDir.path + '/libs') {
        include '*.jar'
        into 'server'
    }
}

task installPlugin() {
    doFirst {
        // Clean
        file(teamCityHomeFolder + "/logs/").list().each() { f -> printAndDelete(teamCityHomeFolder + "/logs/", f) }
        file(teamCityHomeFolder + "/webapps/ROOT/plugins/GlipNotificator/").deleteDir()
        printAndDelete(teamCityBuildServerFolder + '/plugins/', project.name + '.zip')
        file(teamCityBuildServerFolder + "/plugins/.unpacked/" + project.name).deleteDir()
    }

    doLast {
        // And copy!
        Files.copy(Paths.get(buildDir.path + '/distributions/' + project.name + '.zip'),
                Paths.get(teamCityBuildServerFolder + '/plugins/' + project.name + '.zip'))
    }
}

tasks.buildPlugin.dependsOn tasks.shadowJar
tasks.installPlugin.dependsOn tasks.buildPlugin

